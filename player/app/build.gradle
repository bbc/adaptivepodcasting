apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def ENV = System.getenv()

// To avoid just putting the password here, building an APK for upload
// to the app store can be done via the commend line:
// KEYSTORE_PASSWORD=<password> KEYSTORE_FILE=<path-to-keystore.jks> ./gradlew assembleRelease
def KEYSTORE_PASSWORD = ENV['KEYSTORE_PASSWORD'] ?: 'dummy'
def KEYSTORE_FILE = ENV['KEYSTORE_FILE'] ?: 'dummy'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getVersionSuffix = {
    return ENV['VERSION_SUFFIX'] ?: getGitHash()
}

android {
    signingConfigs {
        upload {
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
            keyAlias 'upload'
            keyPassword KEYSTORE_PASSWORD
        }
    }
    compileSdkVersion 33
    defaultConfig {
        applicationId "uk.co.bbc.perceptivepodcasts"
        manifestPlaceholders = [s3hostName:"", s3authKey:""]
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 5
        versionName "1.0.4." + getVersionSuffix()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "adaptive-podcasting-v$versionCode($versionName)"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.upload
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'uk.co.bbc.perceptivepodcasts'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.lifecycle:lifecycle-service:2.5.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'org.kamranzafar:jtar:2.3'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'net.lingala.zip4j:zip4j:2.10.0'
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.datastore:datastore-core:1.0.0'
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.compose.runtime:runtime-livedata:1.3.2"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}
repositories {
    mavenCentral()
}
